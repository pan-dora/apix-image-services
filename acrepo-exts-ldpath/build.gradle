apply plugin: 'osgi'

description = 'LDPath service'
dependencies {
    compile group: 'org.apache.camel', name: 'camel-core', version: camelVersion
    compile group: 'org.apache.camel', name: 'camel-blueprint', version: camelVersion
    compile group: 'org.apache.camel', name: 'camel-jetty9', version: camelVersion
    compile group: 'org.apache.camel', name: 'camel-http4', version: camelVersion
    compile group: 'org.apache.camel', name: 'camel-jackson', version: camelVersion
    compile group: 'org.apache.marmotta', name: 'ldcache-api', version: marmottaVersion
    compile group: 'org.apache.marmotta', name: 'ldpath-core', version: marmottaVersion
    compile group: 'org.apache.marmotta', name: 'ldclient-provider-rdf', version: marmottaVersion
    compile group: 'org.apache.httpcomponents', name:  'httpclient-osgi', version: httpclientVersion
    compile group: 'org.apache.httpcomponents', name:  'httpcore-osgi', version: httpcoreVersion
    compile group: 'org.fcrepo.camel', name: 'fcrepo-ldpath', version: fcrepoCamelToolboxVersion
    compile(group: 'org.fcrepo.camel', name: 'fcrepo-camel', version: fcrepoCamelVersion) {
        exclude(module: 'slf4j-log4j12')
    }
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.apache.camel', name: 'camel-test-blueprint', version: camelVersion
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
}

task buildMainClasses {
    ext.destDir = file('build/classes/main')
    outputs.dir destDir
    doLast {
        destDir.mkdirs()
    }
}

jar {
    dependsOn buildMainClasses
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Import-Package', """
            org.apache.camel,
            org.apache.http.auth,
            org.apache.marmotta.ldcache.api,
            org.apache.marmotta.ldcache.model,
            org.apache.marmotta.ldcache.services,
            org.apache.marmotta.ldclient.api.provider,
            org.apache.marmotta.ldclient.api.endpoint,
            org.apache.marmotta.ldclient.endpoint.rdf,
            org.apache.marmotta.ldclient.provider.rdf,
            org.apache.marmotta.ldpath.backend.linkeddata,
            org.fcrepo.camel.ldpath,
            ${defaultOsgiImports}"""
        instruction 'Export-Package', "edu.amherst.acdc.exts.ldpath;version=${projectOsgiVersion}"
    }
}

artifacts {
    archives (file('build/cfg/main/edu.amherst.acdc.exts.ldpath.cfg')) {
        classifier 'configuration'
        type 'cfg'
    }
}
